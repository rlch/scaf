query GetUser `
MATCH (u:User {id: $userId})
RETURN u.name, u.email, u.age
`

query CreatePost `
CREATE (p:Post {title: $title, authorId: $authorId})
RETURN p.title, p.authorId
`

setup `
MATCH (n) DETACH DELETE n
CREATE (alice:User {id: 1, name: "Alice", email: "alice@example.com", age: 30})
CREATE (bob:User {id: 2, name: "Bob", email: "bob@example.com", age: 25})
CREATE (charlie:User {id: 3, name: "Charlie", email: "charlie@example.com", age: null})
`

teardown `
MATCH (n) DETACH DELETE n
`

GetUser {
	setup `
	CREATE (p:Post {id: 101, authorId: 1, title: "Hello World"})
	`

	group "existing users" {
		test "finds Alice by id" {
			$userId: 1

			u.name: "Alice"
			u.email: "alice@example.com"
			u.age: 30
		}

		test "finds Bob by id" {
			$userId: 2

			u.name: "Bob"
			u.email: "bob@example.com"
			u.age: 25
		}
	}

	group "edge cases" {
		test "user with null age" {
			$userId: 3

			u.name: "Charlie"
			u.age: null
		}

		test "non-existent user returns null" {
			$userId: 999

			u.name: null
			u.email: null
		}
	}

	group "expected failures" {
		test "wrong name fails" {
			$userId: 1

			u.name: "NotAlice"
		}

		test "expecting non-null when null fails" {
			$userId: 3

			u.age: 99
		}
	}
}

CreatePost {
	test "creates a post" {
		$title: "My New Post"
		$authorId: 1

		p.title: "My New Post"
		p.authorId: 1
	}

	test "post creation rolls back" {
		$title: "Temp Post"
		$authorId: 2

		p.title: "Temp Post"
	}
}
